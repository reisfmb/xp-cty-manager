plugins {
    id 'maven-publish'
    id 'com.enonic.defaults' version "2.1.5"
    id 'com.enonic.xp.app' version '3.5.2'
    id "com.github.node-gradle.node" version '7.1.0'
}

app {
    name = "${appName}"
    displayName = "${appDisplayName}"
    vendorName = "${vendorName}"
    vendorUrl = "${vendorUrl}"
    systemVersion = "${xpVersion}"
}

dependencies {
    implementation "com.enonic.xp:core-api:${xpVersion}"
    implementation "com.enonic.xp:portal-api:${xpVersion}"
    include "com.enonic.xp:lib-admin:${xpVersion}"
    include "com.enonic.xp:lib-content:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.lib:lib-thymeleaf:2.1.1"
}

repositories {
    mavenLocal() //Local dev
    mavenCentral()
    xp.enonicRepo('dev')
}

node {
    download = true
    version = '20.9.0'
}

def nodeEnvironment() {
    def environments = [ prod : 'production', dev: 'development' ]
    def nodeEnv = environments[hasProperty( 'env' ) ? env : 'prod']
    return nodeEnv != null ? nodeEnv : 'production'
}

tasks.register('lint', NpmTask) {
    dependsOn( npmInstall )
    description = 'Linting Application.'
    args = [ 'run', 'lint' ]
    inputs.dir 'src/main/resources/admin/tools/vue'
    outputs.dir file('gradle')
    outputs.upToDateWhen { false }
}

tasks.register('webpack', NpmTask ) {
    dependsOn( npmInstall, lint )
    description = 'Building Application.'
    environment = [ 'NODE_ENV': nodeEnvironment() ]
    args = [ 'run', 'build' ]
    inputs.dir 'src/main/resources/admin/tools/vue'
    inputs.file 'webpack.config.js'
    inputs.file 'tsconfig.json'
    inputs.file 'build.gradle'
    inputs.file 'package.json'
    outputs.dir "src/main/resources/assets/js"
    outputs.upToDateWhen { false }
}

tasks.register('tests', NpmTask) {
  dependsOn(webpack)
  description = 'Testing Application.'
  args = [ 'run', 'test' ]
  inputs.dir 'src/main/resources/admin/tools/vue/'
  outputs.dir file('gradle')
  outputs.upToDateWhen { false }
}

processResources {
  dependsOn webpack
}

jar {
    exclude 'admin/tools/vue/**'

    dependsOn webpack, tests
}
